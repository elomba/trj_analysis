module mod_dyn
   !
   ! Module to calculate dynamics properties (MSD, velocity and stress self-correlation,
   ! and dynamic structure factors and intermediate scattering functions, total and self)
   ! Calculations are performed simulteneously over nbuffers separated by jump configurations
   ! with time origins shifted every itlimit configurations (corresponding to tlimit ps)
   !
   use mod_precision
   use mod_common
   use cudafor
   use mod_input
   implicit none
   integer :: ndyn, count_org=0, itlimit
   integer, allocatable, dimension(:) :: origin
   integer, device, allocatable, dimension(:) :: origin_d
   integer, parameter :: nbmax = 1000, nblocks = 5 ! Max. number of buffers, number of blocks to split
   ! operations with particles
   real(myprec), device, allocatable, dimension(:,:,:) :: r0_d, rt_d, v0_d, fktb_d, fsktb_d
   real(myprec), device, allocatable, dimension(:,:) :: r2tb_d, v2tb_d, poff_d, stcorr_d, p0_d
   real(myprec), device, allocatable, dimension(:) :: qw_d
   integer (kind=2), device, allocatable, dimension(:,:) :: indqw_d
   real(myprec), allocatable, dimension(:,:,:) :: r0, v0
   real(myprec),  allocatable, dimension(:,:) :: fkt, fskt, sqw, ssqw
   real(myprec),  allocatable, dimension(:) :: t, v2t
   real(double),  allocatable, dimension(:) :: r2t, stcorr
   real(double), allocatable, dimension(:,:) :: stcorrb, p0
   real(double), allocatable, dimension(:) :: pxy, pxz, pyz
   real(float), allocatable, dimension(:) :: w, ftv
   real(float) :: sumvel=0
contains

   subroutine dyn_init()
      !
      ! Initialize dynamic correlation vars and utils
      !
      if (nbuffer > nbmax) then
         write(*,'("** Error: nbuffer > ",i3,", redimension if possible !")') nbmax
         stop
      endif
      ndyn = Nconf-(nbuffer-1)*jump
      Write(*,"(' *** Using ',i7,' configurations for dynamic analysis: ',&
         i4,' buffers starting every ',i3,' configuration ' )")ndyn, nbuffer, jump
      if (ndyn < 10) then
         write(*,"('** Error: length of dynamics to short. Reduce number of buffers !')")
         stop
      endif
      allocate(t(ndyn),r2t(ndyn))
      allocate(r0(ndim,nmol,nbuffer))
      allocate(origin(nbuffer))
      allocate(origin_d(nbuffer))
      allocate(r0_d(ndim,nmol,nbuffer),rt_d(ndim,nmol,nbuffer),r2tb_d(ndyn,nbuffer))
      r2tb_d(:,:) = 0
      r2t(:) = 0
      if (ex_vel) then
         allocate(v0(ndim,nmol,nbuffer),v2t(ndyn))
         allocate(v0_d(ndim,nmol,nbuffer),v2tb_d(ndyn,nbuffer))
         v2tb_d(:,:) = 0
         v2t(:) = 0
         allocate(w(ndyn),ftv(ndyn))
      endif
      if (ex_stress) then
         allocate(poff_d(1:3,1:nmol),stcorr_d(ndyn,nbuffer),&
            p0_d(1:3,1:nbuffer))
         p0_d(:,:) = 0.0
         allocate(stcorr(ndyn),stcorrb(ndyn,nbuffer),&
            p0(1:3,1:nbuffer))
         allocate(pxy(nmol),pxz(nmol),pyz(nmol))
      endif
      if (run_sqw) then
         allocate(fkt(nqw,ndyn),fskt(nqw,ndyn))
         allocate(sqw(ndyn,nqw),ssqw(ndyn,nqw))
         allocate(fktb_d(nqw,ndyn,nbuffer),fsktb_d(nqw,ndyn,nbuffer),qw_d(nqw))
         qw_d(:) = qw(:)
         fktb_d(:,:,:) = 0.0
         fsktb_d(:,:,:) = 0.0
         fkt(:,:) = 0.0
         fskt(:,:) = 0.0
      endif
   end  subroutine dyn_init

   subroutine dyn_clear()
      ! Deallocate memory
      deallocate(t,r2t)
      deallocate(r0)
      deallocate(r0_d,rt_d,r2tb_d)
      if (ex_vel) then
         deallocate(v0,v2t)
         deallocate(v0_d,v2tb_d)
         deallocate(w,ftv)
      endif
      if (ex_stress) then
         deallocate(stcorr,p0)
         deallocate(stcorr_d,poff_d,p0_d)
      end if
      if (run_sqw) then
         deallocate(fkt,fskt,sqw,ssqw)
         deallocate(fktb_d,fsktb_d)
      endif
   end subroutine dyn_clear

   subroutine rtcorr(iconf)
      !
      ! Calculate averages of time correlation functions
      !
      integer, intent(in) :: iconf
      integer :: istat, iorg, i
      real :: time_gpu, v0(3), v(3)
      ! store times (in ps)
      if (iconf <= ndyn) t(iconf) = nstep*tstep
      ! define buffer origins
      if (iconf==1) then
         do i=1,nbuffer
            origin(i) = (i-1)*jump + 1
         enddo
         itlimit = Nconf
      elseif (iconf==2) then
         if (tlimit<0) then
            itlimit = Nconf-(nbuffer-1)*jump
            tlimit = itlimit*(t(2)-t(1))
         else
            itlimit = nint(tlimit/(t(2)-t(1)))
         endif
         print *, tmax, tlimit, itlimit, nconf, t(2), t(1)
         if (itlimit > Nconf) then
            write(*, "(//' !!! *** Unrecoverable error: tmax=',f8.3,'> trajectory lenght !!')")tmax
            stop
         endif
         if (tmax<0) tmax=tlimit
         if (tmaxp<0) tmaxp=tmax
      endif
      origin_d(:) = origin(:)
      if (ex_vel) then
         v_d(1:ndim, 1:Nmol) = vel(1:ndim,1:Nmol)
      end if
      if (nmol/nblocks<1) then
         write(*,"(' !!** Error: number of particles too small < ',i4)")nblocks
      endif
      istat = cudaEventRecord(startEvent, 0)
      ! Transfer origin positions and velocities for each buffer
      do i = 1, nbuffer
         if (iconf==origin(i)) then
            r0_d(1:ndim,1:nmol,i) = r_d(1:ndim,1:nmol)
            if (ex_vel) v0_d(1:ndim,1:nmol,i) = v_d(1:ndim,1:nmol)
         endif
      enddo

      call rtcal<<<nblocks,nbuffer>>>(ndim,nmol,nbuffer,ndyn,iconf,&
         origin_d)

      if (ex_vel) call vtcal<<<nblocks,nbuffer>>>(ndim,nmol,nbuffer,ndyn,iconf,&
         origin_d)

      if (ex_stress) then
         ! CPU code for viscosity
         pxy(1:nmol) = -stress(4,1:nmol)
         pxz(1:nmol) = -stress(5,1:nmol)
         pyz(1:nmol) = -stress(6,1:nmol)
         do i = 1, nbuffer
            call stcal(nmol,nbuffer,ndyn,iconf,origin,i)
         enddo
         ! CPU code ends here
         !
         ! uncomment for GPU code for viscosity
         !poff_d(1:3,1:nmol) = -stress(4:6,1:nmol)/volumen
         !do i = 1, nbuffer
         !   if (origin(i)==iconf) then
         !      p0_d(1:3,i) = -sum(stress(4:6,1:nmol),dim=2)/volumen
         !   endif
         !enddo
         !call st_cal<<<nblocks,nbuffer>>>(nmol,nbuffer,ndyn,iconf,&
         !             origin_d)
      endif

      if (run_sqw) then
         if (iconf == 1) then
            allocate(indqw_d(nqwmax,ndim))
            indqw_d(:,:) = indqw(:,:)
         endif
         call  FqtnD<<<nqwmax,nbuffer>>>(nqwmax, nqw, Nmol, ndim, itype_d,&
            ntype_d, bsc_d, fk_d, nsp, dq, ndyn, iconf, nbuffer, origin_d)
      endif
      do i = 1, nbuffer
         iorg =  origin(i)
         !  Shift origin position if time limit reached for buffer i
         !  only if itlimit configurations remaining
         if(iconf-iorg==itlimit) iorg=iconf+1
         !
         ! Partial averages are not included in final results, only when itlimit
         ! confs have been read
         !
         if(origin(i) .ne. iorg) then
            ! add up buffers when itlimit reached
            origin(i) = iorg
            r2t(1:ndyn) = r2t(1:ndyn) + r2tb_d(1:ndyn,i)
            r2tb_d(1:ndyn,i) = 0
            if (ex_stress) then
               ! accumulate stress tensor
!               Uncomment for GPU
               !stcorr(1:ndyn) = stcorr(1:ndyn) + stcorr_d(1:ndyn,i)
               !stcorr_d(1:ndyn,i) = 0.0
               ! CPU code
               stcorr(1:ndyn) = stcorr(1:ndyn) + stcorrb(1:ndyn,i)
               stcorrb(1:ndyn,i) = 0.0
            endif
            if (ex_vel) then
               v2t(1:ndyn) = v2t(1:ndyn) +v2tb_d(1:ndyn,i)
               v2tb_d(1:ndyn,i) = 0.0
            endif
            if (run_sqw) then
               fkt(1:nqw,1:ndyn) = fkt(1:nqw,1:ndyn)+fktb_d(1:nqw,1:ndyn,i)
               fskt(1:nqw,1:ndyn) = fskt(1:nqw,1:ndyn)+fsktb_d(1:nqw,1:ndyn,i)
               fktb_d(1:nqw,1:ndyn,i) = 0
               fsktb_d(1:nqw,1:ndyn,i) = 0
            endif
            count_org = count_org+1
         endif
      enddo
      time_gpu = gptime(stopEvent, startEvent)
      tdyn = tdyn + time_gpu
      call printcudaerror(" **! rtcorr GPU kernel exit")
   end subroutine rtcorr

   subroutine print_rtcor()
      !
      ! Print out time correlations
      !
      use fftw3
      implicit none
      integer :: i, j
      real :: fconv=4.135
      real :: sum_visc=0.0, pct, pct1, alpha=10.0, fw
      t(1:ndyn) = t(1:ndyn)-t(1)
      write(*,"(/'** Length of dynamics ',f8.3,' ps, ',i7,' configurations, '&
         ,i5,' time origins'/)")t(ndyn),ndyn, count_org
      r2t(:) = r2t(:)/count_org
      if (ex_vel) then
         v2t(:) = v2t(:)/count_org
         call fftw1d(v2t,ftv,w,ndyn,t(2)-t(1),tmax)
      end if
      if (run_sqw) then
         !
         ! set q-dependent time window for F(q,t) transforms to tmx
         if (maxval(tmqw(:))<1.0e-6) then
            tmqw(:) = tmax
         endif
      endif
      open(55,file="dyn.dat")
      if(ex_vel) open(555,file="dynw.dat")
      if (run_sqw) then
         open(65,file='fkt.dat')
         open(66,file='fskt.dat')
         open(67,file='sqw.dat')
         do i=1, nqw
            fkt(i,1:ndyn)=fkt(i,1:ndyn)/(Nmol*count_org*nw(i))
            fskt(i,1:ndyn)=fskt(i,1:ndyn)/(Nmol*count_org*nw(i))
            call fftw1d(fkt(i,1:ndyn),sqw(1:ndyn,i),w,ndyn,t(2)-t(1),tmax)
            call fftw1d(fskt(i,1:ndyn),ssqw(1:ndyn,i),w,ndyn,t(2)-t(1),tmax)
         enddo
      endif
      if (ex_stress) then
         open(56,file="viscor.dat")
         write(56,"('#',2x,'t(ps)',9x,'P_off(t)',7x,'eta_GK(t) (bar*ps))')")
      endif
      if (ex_vel) then
         if (tunits == 'lj') then
            write(55,"('#',2x,'t(tau)',9x,'C_r(t)',7x'C_v(t)')')")
            write(555,"('#',9x,'w(tau^-1)        Z(w)')")
         else
            write(55,"('#',2x,'t(ps)',9x,'C_r(t)',7x'C_v(t)')')")
            write(555,"('#',9x,'w(THz)',7x,'w(meV)',12x,'Z(w)')")
         endif
      else
         if (tunits == 'lj') then
            write(55,"('#',2x,'t(tau)',9x,'C_r(t)')")
         else  
            write(55,"('#',2x,'t(ps)',9x,'C_r(t)')")
         endif
      endif
      if (run_sqw) then
         if (tunits == 'lj') then
            write(65,'("#",2x,"t(tau)",9x,8("F(",f6.4,",t)",4x:))')qw(1:nqw)
            write(66,'("#",2x,"t(tau)",9x,8("Fs(",f6.4,",t)",4x:))')qw(1:nqw)
            write(67,"('#',2x,'w(1/tau)',5x,8('S(',f6.4,',w)    Ss(',f6.4,',w)',4x:))')")(qw(j),qw(j),j=1,nqw)
         else
            write(65,'("#",2x,"t(ps)",9x,8("F(",f6.4,",t)",4x:))')qw(1:nqw)
            write(66,'("#",2x,"t(ps)",9x,8("Fs(",f6.4,",t)",4x:))')qw(1:nqw)
            write(67,"('#',2x,'w(THz)',7x,'w(meV)',5x,8('S(',f6.4,',w)    Ss(',f6.4,',w)',4x:))')")(qw(j),qw(j),j=1,nqw)
         endif
      endif
      do i = 1, ndyn
         if (ex_vel) then
            !
            ! transform from w(wavenumber) to f (frequency) in THz and meV
            !
            if (i<=itlimit) write(55,"(f10.3,8f15.5)")t(i), r2t(i),v2t(i)
            if (tunits=='lj') then
               write(555,"(8f15.5)")w(i)/(2*pi),ftv(i)
            else 
               write(555,"(8f15.5)")w(i)/(2*pi),fconv*w(i)/(2*pi),ftv(i)
            endif

         else
            if(i<=itlimit) write(55,"(f10.3,8f15.5)")t(i), r2t(i)
         endif
         if (ex_stress) then
            fw = 0.5*(1-tanh((alpha/tmaxP)*(t(i)-tmaxP)))
            if (i==1) then
               pct =  fw*stcorr(i)/(ndim*count_org)
               write(56,"(f10.3,8f15.5)")t(i),pct,0.0
            else
               pct1 = pct

               pct =  fw*stcorr(i)/(ndim*count_org)
               ! Integrate off-diagonal pressure tensor correlation (trapezoidal rule)
               ! Use Green-Kubo relation eta_gk = (V/NkT)*\int_0^t<pxy(t')pxy(0)>dt' in (bar*s)
               !
               sum_visc = sum_visc + (pct+pct1)*(t(i)-t(i-1))/2
               if (i<=itlimit) write(56,"(f10.3,9f15.5)")t(i),pct,(volumen/temperature/c_nktv)*sum_visc
            endif
         end if
         if (run_sqw) then
            if (i<=itlimit) then
               write(65,"(f10.3,8f15.5)")t(i),fkt(1:nqw,i)
               write(66,"(f10.3,8f15.5)")t(i),fskt(1:nqw,i)
            endif
            if (tunits == 'lj') then
               write(67,"(f10.3,8f15.5)")w(i)/(2*pi),(sqw(i,j),ssqw(i,j),j=1,nqw)
            else
               write(67,"(2f10.3,8f15.5)")w(i)/(2*pi),fconv*w(i)/(2*pi),(sqw(i,j),ssqw(i,j),j=1,nqw)
            endif
         endif
      end do
      close(55)
      close(65)
      close(66)
      close(67)
      close(555)
   end subroutine print_rtcor

   subroutine stcal(mol,buffer,dyn,iconf,origin, i)
      !
      ! off-diagonal pressure tensor time correlation. CPU version
      !
      integer, value, intent(IN) :: mol, buffer, dyn, iconf, i
      real(double) :: sumaxy, sumayz, sumaxz, sumaxx, sumayy, sumazz, suma
      integer :: j, k, npsize, nprest, ia, indx, iorg
      integer, dimension(buffer), intent(in) :: origin
      iorg = origin(i)
      indx = iconf - iorg + 1
! Check origin and final configuration of each averaging buffer
      if (iconf>=iorg .and. indx <= dyn) then
         sumaxy = sum(pxy(1:mol))/volumen
         sumaxz = sum(pxz(1:mol))/volumen
         sumayz = sum(pyz(1:mol))/volumen
         if (iorg==iconf) then
            p0(1,i) = sumaxy
            p0(2,i) = sumaxz
            p0(3,i) = sumayz
         end if
         suma = sumaxy*p0(1,i)+sumaxz*p0(2,i)+sumayz*p0(3,i)
         stcorrb(indx,i) = stcorrb(indx,i)+suma
      endif
   end subroutine stcal


   attributes(global) subroutine FqtnD(nqwmax, nqw, Nmol, dim, itype, ntype, &
      bsc, fk, nsp, dq, dyn, iconf, buffer, origin)
   !
   ! F(Q,t): GPU version parallelization over Q vectors
   !
   integer, intent(IN) :: itype(Nmol), ntype(nsp)
   integer, value, intent(IN) :: Nmol, dim, nsp, nqwmax, dyn, iconf,&
      nqw, buffer
   integer :: ibuffer, indx
   real(myprec), intent(IN) :: bsc(nsp)
   real(myprec) :: rkxyz, rkxyzt, fk1, fk2, fk3, kf1, kf2, kf3, tsum, q, suma
   real(double) :: sumcx, sumsx, sumcpx, sumspx, q2, bs
   real(double) :: sumcxt, sumsxt, sumcpxt, sumspxt, sumself
   real(myprec), intent(IN) :: fk(dim)
   real(myprec), value, intent(IN) ::  dq
   integer, dimension(buffer), intent(in) :: origin

   integer :: i, j, it, kx, ky, kz, ind, ia, iorg
   logical :: found
   if (blockidx%x<=nqwmax) then
      ibuffer = threadidx%x
      iorg =  origin(ibuffer)
      indx = iconf - iorg + 1
      ! Check origin and final configuration of each averaging buffer
      if (iconf>=iorg .and. indx >= 1) then
         kx = indqw_d(blockidx%x,1)
         ky = indqw_d(blockidx%x,2)
         fk1 = fk(1)
         fk2 = fk(2)
         kf1 = kx*fk1
         kf2 = ky*fk2
         if (dim==3) then
            kz = indqw_d(blockidx%x,3)
            fk3 = fk(3)
            kf3 = kz*fk3
            q2 = kf1*kf1 + kf2*kf2 + kf3*kf3
         else
            q2 = kf1*kf1 + kf2*kf2
         endif
         sumcx = 0
         sumcxt = 0
         sumsx = 0
         sumsxt = 0
         sumself = 0
         q = sqrt(q2)
         ind = 1
         found = .false.
         do while (ind<=nqw.and. .not. found)
            if (qw_d(ind)-dq/2<q .and. qw_d(ind)+dq/2>q) then
               found = .true.
            else
               ind = ind+1
            endif
         enddo
         i = 0
         do j = 1, nsp
            sumcpx = 0
            sumspx = 0
            sumcpxt = 0
            sumspxt = 0
            bs = bsc(j)
            do it = 1, ntype(j)
               i = i + 1
               if (dim==3) then
                  rkxyz = r0_d(3, i,ibuffer)*kf3+r0_d(2, i,ibuffer)*kf2+r0_d(1, i,ibuffer)*kf1
                  rkxyzt = r_d(3, i)*kf3+r_d(2, i)*kf2+r_d(1, i)*kf1
               else
                  rkxyz = r0_d(2, i,ibuffer)*kf2+r0_d(1, i,ibuffer)*kf1
                  rkxyzt = r_d(2, i)*kf2+r_d(1, i)*kf1
               endif
               sumcpx = sumcpx + __cosf(rkxyz)
               sumspx = sumspx + __sinf(rkxyz)
               sumcpxt = sumcpxt + __cosf(rkxyzt)
               sumspxt = sumspxt + __sinf(rkxyzt)
               sumself = sumself + __cosf(rkxyzt-rkxyz)
            end do
            sumcx = sumcx + bs*sumcpx
            sumcxt = sumcxt + bs*sumcpxt
            sumsx = sumsx + bs*sumspx
            sumsxt = sumsxt + bs*sumspxt
         end do
         tsum = (sumcx*sumcxt + sumsx*sumsxt)
         ia = atomicadd(fktb_d(ind,indx,ibuffer), tsum)
         ia = atomicadd(fsktb_d(ind,indx,ibuffer), real(sumself,kind=4))
      endif
   endif
end subroutine FqtnD


attributes(global) subroutine rtcal(dim,mol,buffer,dyn,iconf,origin)
!
!   C(t,t0) = <r(t)r(t0)> time correlation. Parallelization over buffers (time origin)
!   (threads) and particles (blocks)
!
integer, value, intent(IN) :: mol, dim, buffer, dyn, iconf
real :: rv(3), suma
real(double) :: suma8
integer :: i, j, k, npsize, nprest, ia, indx, iorg, jmax
integer, dimension(buffer), intent(in) :: origin
i = threadidx%x
iorg = origin(i)
indx = iconf - iorg + 1
! Check origin and final configuration of each averaging buffer
if (iconf==iorg) rt_d(1:dim,1:mol,i) = r_d(1:dim,1:mol)

if (iconf>=iorg .and. indx <= dyn) then
   suma8 = 0
   npsize = mol/nblocks
   nprest = mod(mol,nblocks)
   ! Unwrap coordinates
   do k = 1, dim
      rt_d(k,1:mol,i)= r_d(k,1:mol) +sidel_d(k)&
      &  *nint((rt_d(k,1:mol,i)-r_d(k,1:mol))/sidel_d(k))
   end do
   suma8 = 0
   jmax = blockidx%x*npsize
   if (jmax + nprest == mol) jmax = mol
   do j = (blockidx%x - 1)*npsize+1, jmax
      rv(1:dim) = rt_d(1:dim,j,i)-r0_d(1:dim,j,i)
      suma8 = dot_product(rv,rv) + suma8
   end do
   suma = suma8/mol
   if (indx <= dyn .and. i <= buffer) then
      ia = atomicadd(r2tb_d(indx,i),suma)
   endif
endif
end subroutine rtcal

attributes(global) subroutine st_cal(mol,buffer,dyn,iconf,&
   origin)
integer, value, intent(IN) :: mol, buffer, dyn, iconf
real :: suma
real(double) :: sumaxy, sumayz, sumaxz, sumaxx, sumayy, sumazz
integer :: i, j, k, npsize, nprest, ia, indx, iorg, jmax
integer, dimension(buffer), intent(in) :: origin
i = threadidx%x
iorg = origin(i)
indx = iconf - iorg + 1
! Check origin and final configuration of each averaging buffer
if (iconf>=iorg .and. indx <= dyn) then
   npsize = mol/nblocks
   nprest = mod(mol,nblocks)
   sumaxy = 0.0d0
   sumaxz = 0.0d0
   sumayz = 0.0d0
   jmax = blockidx%x*npsize
   if (jmax + nprest == mol) jmax = mol
   do j = (blockidx%x - 1)*npsize+1, jmax
      sumaxy = sumaxy + poff_d(1,j)
      sumaxz = sumaxz + poff_d(2,j)
      sumayz = sumayz + poff_d(3,j)
   enddo
   suma = sumaxy*p0_d(1,i)+sumaxz*p0_d(2,i)+sumayz*p0_d(3,i)
   ia = atomicadd(stcorr_d(indx,i),suma)
endif
end subroutine st_cal

attributes(global) subroutine vtcal(dim,mol,buffer,dyn,iconf,origin)
integer, value, intent(IN) :: mol, dim, buffer, dyn, iconf
real(double) :: suma8
real :: sumf
integer :: i, j, k, npsize, nprest, ia, indx, iorg, jmax
integer, dimension(buffer), intent(in) :: origin
i = threadidx%x
iorg = origin(i)
indx = iconf - iorg + 1
! Check origin and final configuration of each averaging buffer
if (iconf>=iorg .and. indx >= 1) then
   npsize = mol/nblocks
   nprest = mod(mol,nblocks)
   suma8 = 0.0d0
   jmax = blockidx%x*npsize
   if (jmax + nprest == mol) jmax = mol
   do j = (blockidx%x - 1)*npsize+1, jmax
      suma8 = dot_product(v_d(1:dim,j),v0_d(1:dim,j,i)) + suma8
   end do
   sumf = suma8/mol
   ia = atomicadd(v2tb_d(indx,i),sumf)
endif
end subroutine vtcal

end module mod_dyn
