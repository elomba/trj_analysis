module mod_dyn
   use mod_precision
   use mod_common
   use cudafor
   use mod_input 
   implicit none
   integer :: ndyn
   integer, parameter :: nbmax = 100, nblocks = 1
   real(myprec), device, allocatable, dimension(:,:,:) :: r0_d, rt_d, v0_d
   real(myprec), device, allocatable, dimension(:,:) :: r2tb_d, v2tb_d
   real(myprec), allocatable, dimension(:,:,:) :: r0, v0
   real(myprec),  allocatable, dimension(:,:) :: r2tb, v2tb
   real(myprec),  allocatable, dimension(:) :: t, r2t, v2t
   real(float), allocatable, dimension(:) :: w, ftv
   real(float) :: sumvel=0 
   contains

   subroutine dyn_init()
      if (nbuffer > nbmax) then
         write(*,'("** Error: nbuffer > ",i3,", redimension if possible !")') nbmax
         stop
      endif
      ndyn = Nconf
      if (ndyn < 10) then
         write(*,"('** Error: length of dynamics to short. Reduce number of buffers !')")
         stop
      endif
      allocate(t(ndyn),r2t(ndyn))
      allocate(r0(ndim,nmol,nbuffer),r2tb(ndyn,nbuffer))
      allocate(r0_d(ndim,nmol,nbuffer),rt_d(ndim,nmol,nbuffer),r2tb_d(ndyn,nbuffer))
      r2tb_d(:,:) = 0
      if (keytrj > 0) then
         allocate(v0(ndim,nmol,nbuffer),v2t(ndyn),v2tb(ndyn,nbuffer))
         allocate(v0_d(ndim,nmol,nbuffer),v2tb_d(ndyn,nbuffer))
         v2tb_d(:,:) = 0
         allocate(w(ndyn),ftv(ndyn))
      endif
   end  subroutine dyn_init

   subroutine dyn_clear()
      deallocate(t,r2t)
      deallocate(r0,r2tb)
      deallocate(r0_d,rt_d,r2tb_d)
      if (keytrj > 0) then
         deallocate(v0,v2t,v2tb)
         deallocate(v0_d,v2tb_d)
         deallocate(w,ftv)
      endif
   end subroutine dyn_clear

   subroutine rtcorr(iconf)
      integer, intent(in) :: iconf
      integer :: i
      integer :: istat
      real :: time_gpu, v0(3), v(3), res
      if (keytrj>0) then
         v_d(1:ndim, 1:Nmol) = vel(1:ndim,1:Nmol)
      end if
      if (nmol/nblocks<1) then
         write(*,"('** Error: number of particles too small < ',i4)")nblocks
      endif 
      if (iconf <= nbuffer) then
         r0_d(1:ndim,1:nmol,iconf) = r_d(1:ndim,1:nmol)
         if (keytrj>0) then
            v0_d(1:ndim,1:nmol,iconf) = v_d(1:ndim,1:nmol)
         end if

      endif
      if (iconf <= ndyn) t(iconf) = nstep*tstep
      istat = cudaEventRecord(startEvent, 0)
      call rtcal<<<nblocks,nbuffer>>>(ndim,nmol,nbuffer,ndyn,iconf)
      if (keytrj>0) then
         call vtcal<<<nblocks,nbuffer>>>(ndim,nmol,nbuffer,ndyn,iconf)
      end if 
      time_gpu = gptime(stopEvent, startEvent)
      tdyn = tdyn + time_gpu
   end subroutine rtcorr

   subroutine print_rtcor()
      use fftw3
      implicit none
      integer :: i
      real :: fconv=4.135
      r2tb(1:ndyn,1:nbuffer) = r2tb_d(1:ndyn,1:nbuffer)
      r2t(1:ndyn) = sum(r2tb(1:ndyn,1:nbuffer),DIM=2)
      if (keytrj>0) then
         v2tb(1:ndyn,1:nbuffer) = v2tb_d(1:ndyn,1:nbuffer)
         v2t(1:ndyn) = sum(v2tb(1:ndyn,1:nbuffer),DIM=2)
      end if 
      t(1:ndyn) = t(1:ndyn)-t(1)
      r2t(:) = r2t(:)/nbuffer
      if (keytrj>0) then
         v2t(:) = v2t(:)/nbuffer
         call fftw1d(v2t,ftv,w,ndyn,t(2)-t(1),tmax)
      end if 
      open(55,file="dyn.dat")
      if (keytrj>0) then
          write(55,"('#',2x,'t(ps)',9x,'C_r(t)',7x'C_v(t)',10x,'f(THz)',7x,'f(meV)',12x,'F(w)')")
      else
         write(55,"('#',2x,'t(ps)',9x,'C_r(t)')")
      endif
      do i = 1, Nconf - nbuffer
         if (keytrj > 0) then
            !
            ! transform from w(wavenumber) to f (frequency) in THz and meV
            !
             write(55,"(f10.3,8f15.5)")t(i), r2t(i),v2t(i),w(i)/(2*pi),fconv*w(i)/(2*pi),ftv(i)
         else
            write(55,"(f10.3,8f15.5)")t(i), r2t(i)
         endif 
      end do
      close(55)
   end subroutine print_rtcor



   attributes(global) subroutine vtcal(dim,mol,buffer,dyn,iconf)
   integer, value, intent(IN) :: mol, dim, buffer, dyn, iconf
   real(double) :: suma8
   real :: sumf 
   integer :: i, j, k, npsize, nprest, ia, indx 
   i = threadidx%x
   indx = iconf-i+1
   ! Check origin and final configuration of each averaging buffer 
   if (iconf<i.or. indx>dyn) return
   npsize = mol/nblocks
   nprest = mod(mol,nblocks)
   suma8 = 0.0d0
   if (iconf>=i) then
      do j = (blockidx%x - 1)*npsize+1, blockidx%x*npsize
         suma8 = dot_product(v_d(1:dim,j),v0_d(1:dim,j,i)) + suma8 
      end do
      do j = blockidx%x*npsize+1, blockidx%x*npsize+nprest
         suma8 = dot_product(v_d(1:dim,j),v0_d(1:dim,j,i)) + suma8 
      end do
      sumf = suma8/mol
      ia = atomicadd(v2tb_d(indx,i),sumf)
   endif 
end subroutine vtcal
   


   attributes(global) subroutine rtcal(dim,mol,buffer,dyn,iconf)
   integer, value, intent(IN) :: mol, dim, buffer, dyn, iconf
   real :: rv(3), suma
   real(double) :: suma8
   integer :: i, j, k, npsize, nprest, ia, indx 
   i = threadidx%x
   indx = iconf -i + 1
   ! Check origin and final configuration of each averaging buffer 
   if (iconf<i .or. indx>dyn) return
   suma8 = 0
   npsize = mol/nblocks
   nprest = mod(mol,nblocks)
   if (i==iconf) then
      rt_d(1:dim,1:mol,i) = r_d(1:dim,1:mol)
   end if
   if (iconf>i) then
      ! Unwrap coordinates
      do k = 1, dim
         rt_d(k,1:mol,i)= r_d(k,1:mol) +sidel_d(k)&
                          & *nint((rt_d(k,1:mol,i)-r_d(k,1:mol))/sidel_d(k))
      end do
      suma8 = 0
      do j = (blockidx%x - 1)*npsize+1, blockidx%x*npsize
         rv(1:dim) = rt_d(1:dim,j,i)-r0_d(1:dim,j,i)
         suma8 = dot_product(rv,rv) + suma8 
      end do
      do j = blockidx%x*npsize+1, blockidx%x*npsize+nprest
         rv(1:dim) = rt_d(1:dim,j,i)-r0_d(1:dim,j,i)
         suma8 = dot_product(rv,rv)  + suma8
      end do
      suma = suma8/mol 
      ia = atomicadd(r2tb_d(indx,i),suma)
   endif 
end subroutine rtcal
end module mod_dyn
